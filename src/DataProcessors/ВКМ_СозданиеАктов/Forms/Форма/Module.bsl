
&НаКлиенте
Процедура СоздатьАкты(Команда) 
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выберите период!");
		Возврат;
	КонецЕсли;
	
	МассивДоговоров = Новый Массив;
	Для Каждого СтрокаТЧ Из Объект.СписокДоговоров Цикл
		МассивДоговоров.Добавить(СтрокаТЧ.Договор);
	КонецЦикла;
	
	Если МассивДоговоров.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Список договоров для обработки пуст.");
		Возврат;
	КонецЕсли; 
	
	
	ДлительнаяОперация = СоздатьАктыНаСервере(Объект.Период, МассивДоговоров);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	ОписаниеОЗавершении = Новый ОписаниеОповещения("ОписаниеОЗавершении", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция  СоздатьАктыНаСервере(Период, МассивДоговоров)
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор,
		"Обработки.ВКМ_СозданиеАктов.СоздатьАкты", Период, МассивДоговоров);
	Возврат ДлительнаяОперация;
КонецФункции


&НаКлиенте
Процедура ОписаниеОЗавершении(Результат, ДополнительныеПараметры) Экспорт  
	
	Если Результат = Неопределено Тогда 
		Возврат; 	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
				МассивРезультатов = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Если МассивРезультатов <> Неопределено Тогда
			
			Для Каждого РезультатСтрока Из МассивРезультатов Цикл
				
				Поиск = Новый Структура("Договор", РезультатСтрока.Договор);
				НайденныеСтроки = Объект.СписокДоговоров.НайтиСтроки(Поиск);
				
				Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
					СтрокаТЧ.Реализация = РезультатСтрока.Реализация;
				КонецЦикла;
				
			КонецЦикла;
			
			ПоказатьОповещениеПользователя("Успех", , "Документы созданы и отражены в списке.");
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Произошла ошибка: " + Результат.ПодробноеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры
