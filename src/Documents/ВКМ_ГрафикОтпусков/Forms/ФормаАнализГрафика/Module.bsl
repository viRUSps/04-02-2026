
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Если Параметры.Свойство("АдресДанных") Тогда
        ДанныеТЗ = ПолучитьИзВременногоХранилища(Параметры.АдресДанных);
        
        ГодДокумента = 0;
        Параметры.Свойство("ТекущийГод", ГодДокумента);
        
        ПостроитьДиаграмму(ДанныеТЗ, ГодДокумента);
    КонецЕсли;    	
КонецПроцедуры

&НаСервере
Процедура ПостроитьДиаграмму(ДанныеТЗ, ГодДокумента)
	
	ЧислоВыбранногоГода = ?(ТипЗнч(ГодДокумента) = Тип("Дата"), Год(ГодДокумента), Число(ГодДокумента)); 
	
	 Объект.ГодОтпуска =  ГодДокумента;
    
    ЭтаФорма.АвтоЗаголовок = Ложь;
    ЭтаФорма.Заголовок = "Анализ графика отпусков на " + Формат(ЧислоВыбранногоГода, "ЧГ=0");
	ЭтаФорма.Заголовок = "Анализ графика отпусков на " + Формат(ЧислоВыбранногоГода, "ЧГ=0");
	
    ДиаграммаГанта.Обновление = Ложь; 
    ДиаграммаГанта.Очистить(); 
    
    ДиаграммаГанта.ОтображатьЛегенду = Ложь; 

    НачалоГрафика = Дата(ЧислоВыбранногоГода, 1, 1);
    КонецГрафика  = КонецГода(НачалоГрафика);
    ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь; 
    ДиаграммаГанта.УстановитьПолныйИнтервал(НачалоГрафика, КонецГрафика);
    
    СерияОтпуска = ДиаграммаГанта.УстановитьСерию("Отпуска"); 
    СоответствиеТочек = Новый Соответствие;   
	
	 Шкала = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени;
    
    Если Шкала.Элементы.Количество() > 0 Тогда
        ЭлементМесяц = Шкала.Элементы[0];
        ЭлементМесяц.Единица = ТипЕдиницыШкалыВремени.Месяц;
        ЭлементМесяц.Кратность = 1;
        ЭлементМесяц.Формат = "ДФ='MMM yy'";
        
        Если Шкала.Элементы.Количество() > 1 Тогда
            Шкала.Элементы.Удалить(1);
        КонецЕсли;
    Иначе
        НовыйЭлемент = Шкала.Элементы.Добавить();
        НовыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Месяц;
        НовыйЭлемент.Кратность = 1;
    КонецЕсли;	
	
    Для Каждого Строка Из ДанныеТЗ Цикл
        
        Точка = СоответствиеТочек[Строка.Сотрудник];
        Если Точка = Неопределено Тогда
            Точка = ДиаграммаГанта.УстановитьТочку(Строка.Сотрудник); 
            Точка.Текст = Строка(Строка.Сотрудник); 
            СоответствиеТочек.Вставить(Строка.Сотрудник, Точка);
        КонецЕсли;
        
        Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, СерияОтпуска);
        
        ТекстИнтервала = Формат(Строка.ДатаНачала, "ДФ=dd.MM") + "-" + Формат(Строка.ДатаОкончания, "ДФ=dd.MM");
        
        Если ПустаяСтрока(Значение.Текст) Тогда
            Значение.Текст = ТекстИнтервала;
        Иначе
            Значение.Текст = Значение.Текст + "; " + ТекстИнтервала;
        КонецЕсли;
        
        Интервал = Значение.Добавить();
        Интервал.Начало = НачалоДня(Строка.ДатаНачала); 
        Интервал.Конец  = КонецДня(Строка.ДатаОкончания); 
        Интервал.Текст  = ТекстИнтервала; 
        
    КонецЦикла;  

    ДиаграммаГанта.Обновление = Истина;	

КонецПроцедуры
	
