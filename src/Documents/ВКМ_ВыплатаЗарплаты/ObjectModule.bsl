
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
    Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
    Движения.ВКМ_ВзаиморасчетыССотрудниками.Очистить();
    
    ТЧ_Контроль = Выплаты.Выгрузить();
    ТЧ_Контроль.Свернуть("Сотрудник", "Сумма");
    
    Запрос = Новый Запрос;
    Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    
    Запрос.Текст = 
    "ВЫБРАТЬ
    |	ТЧ.Сотрудник КАК Сотрудник,
    |	ТЧ.Сумма КАК Сумма
    |ПОМЕСТИТЬ ВТ_Выплаты
    |ИЗ
    |	&ТЧ КАК ТЧ
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ
    |	ВТ_Выплаты.Сотрудник,
    |	ВТ_Выплаты.Сумма КАК СуммаВыплаты,
    |	ЕСТЬNULL(Остатки.СуммаОстаток, 0) КАК Долг
    |ИЗ
    |	ВТ_Выплаты КАК ВТ_Выплаты
    |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&Момент, ) КАК Остатки
    |		ПО ВТ_Выплаты.Сотрудник = Остатки.Сотрудник";
    
    Запрос.УстановитьПараметр("ТЧ", ТЧ_Контроль);
    Запрос.УстановитьПараметр("Момент", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
    
    РезультатЗапроса = Запрос.Выполнить();
    
    Если НЕ РезультатЗапроса.Пустой() Тогда
        Выборка = РезультатЗапроса.Выбрать();
        
        Пока Выборка.Следующий() Цикл
            Если Выборка.СуммаВыплаты > Выборка.Долг Тогда
                Сообщение = Новый СообщениеПользователю;
                Сообщение.Текст = "Превышен лимит выплаты для: " + Выборка.Сотрудник + 
                                  " (К выплате: " + Выборка.СуммаВыплаты + ", Долг: " + Выборка.Долг + ")";
                Сообщение.Сообщить();
                Отказ = Истина;
            КонецЕсли;
        КонецЦикла;
    КонецЕсли;
    
    Если НЕ Отказ Тогда
        Для Каждого Строка Из Выплаты Цикл
			Если 
				Строка.Сотрудник.Пустая() ИЛИ Строка.Сумма = 0 Тогда Продолжить;
			КонецЕсли;
            
            Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
            Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
            Движение.Период      = Дата;
            Движение.Сотрудник   = Строка.Сотрудник;
            Движение.Сумма       = Строка.Сумма;
        КонецЦикла;
    КонецЕсли;

КонецПроцедуры

